import groovy.json.JsonSlurper

description = "Mongoose web storage mock"

buildscript {
	dependencies {
		classpath "com.google.gradle:osdetector-gradle-plugin:1.4.0"
	}
	repositories {
		mavenCentral()
	}
}

apply plugin: "java"
apply plugin: "maven"
apply plugin: "signing"
apply plugin: "com.google.osdetector"

group = "com.github.emc-mongoose"
version = defineVersion()

tasks.withType(JavaCompile) {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

task("create-dirs").doLast(
	{
		sourceSets*.java.srcDirs*.each { it.mkdirs() }
		sourceSets*.resources.srcDirs*.each { it.mkdirs() }
	}
)

repositories {
	mavenCentral()
}

ext {

	depVersion = [
		commonsCodec      : "1.10",
		commonsCollections: "4.1",
		commonsJava       : "1.0.2",
		commonsLang       : "2.6",
		disruptor         : "3.3.4",
		jackson           : "2.8.9",
		javassist         : "3.21.0-GA",
		slf4j             : "1.7.24",
		log4j             : "2.8.2",
		metrics           : "3.2.2",
		mongoose          : "3.5.0-alpha3",
		netty             : "4.1.12.Final",
	]

	setClassPath = {
		configurations -> [
			configurations.runtime
				.collect { file ->
				// it's 3rd party if jar file name doesn't contain the root project name
				if(-1 == file.name.indexOf(rootProject.name)) {
					["lib", file.name].join(File.separator)
				} else {
					file.name
				}
			}
			.join(" "),
		]
			.join(" ")
	}
}

dependencies {
	compile(
		"com.github.emc-mongoose:mongoose-api-common:${depVersion.mongoose}",
		"com.github.emc-mongoose:mongoose-api-model:${depVersion.mongoose}",
		"com.github.emc-mongoose:mongoose-api-metrics:${depVersion.mongoose}",
		"com.github.emc-mongoose:mongoose-ui:${depVersion.mongoose}",
		/*files(
			"../mongoose/api/common/build/libs/mongoose-api-common.jar",
			"../mongoose/api/model/build/libs/mongoose-api-model.jar",
			"../mongoose/api/metrics/build/libs/mongoose-api-metrics.jar",
			"../mongoose/ui/build/libs/mongoose-ui.jar"
		),*/

		"com.github.akurilov:java-commons:${depVersion.commonsJava}",
		"commons-lang:commons-lang:${depVersion.commonsLang}",
		"org.apache.commons:commons-collections4:${depVersion.commonsCollections}",
		"commons-codec:commons-codec:${depVersion.commonsCodec}",
		"org.slf4j:slf4j-api:${depVersion.slf4j}",
		"io.dropwizard.metrics:metrics-core:${depVersion.metrics}",

		"org.apache.logging.log4j:log4j-api:${depVersion.log4j}",
		"org.apache.logging.log4j:log4j-core:${depVersion.log4j}",

		"com.fasterxml.jackson.core:jackson-databind:${depVersion.jackson}",

		"io.netty:netty-common:${depVersion.netty}",
		"io.netty:netty-buffer:${depVersion.netty}",
		"io.netty:netty-transport:${depVersion.netty}",
		"io.netty:netty-handler:${depVersion.netty}",
		"io.netty:netty-codec-http:${depVersion.netty}",
		"io.netty:netty-transport-native-epoll:${depVersion.netty}:${osdetector.classifier}",

		"org.jmdns:jmdns:3.5.1"
	)

	runtime(
		"com.lmax:disruptor:${depVersion.disruptor}",
		"org.apache.logging.log4j:log4j-jul:${depVersion.log4j}",
		"org.apache.logging.log4j:log4j-slf4j-impl:${depVersion.log4j}",
		"org.javassist:javassist:${depVersion.javassist}",
	)

	testCompile(
		"junit:junit:[4,)",
		"com.github.fge:json-schema-validator:[2,)",
		"org.apache.logging.log4j:log4j-api:${depVersion.log4j}",
		"org.apache.logging.log4j:log4j-core:${depVersion.log4j}",
	)
}

test {
	/*
	systemProperty "com.sun.management.jmxremote", "true"
	systemProperty "com.sun.management.jmxremote.port", "9010"
	systemProperty "com.sun.management.jmxremote.local.only", "false"
	systemProperty "com.sun.management.jmxremote.authenticate", "false"
	systemProperty "com.sun.management.jmxremote.ssl", "false"
	*/
	testLogging {
		events "passed", "skipped", "failed", "standardOut"
		showExceptions = true
		showStandardStreams = true
	}
}

jar {
	archiveName "$project.name.$extension"
	manifest {
		attributes(
			"Class-Path": setClassPath(configurations),
			"Main-Class": "com.emc.mongoose.storage.mock.Main",
			"Implementation-Version": version,
			"Implementation-Title": "$name"
		)
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	archiveName "$project.name-$classifier.$extension"
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = "javadoc"
	archiveName "$project.name-$classifier.$extension"
	from javadoc.destinationDir
}

artifacts {
	archives jar
	archives sourcesJar
	archives javadocJar
}

def ossrhUsername = project.hasProperty("ossrhUsername") ?
	project.property("ossrhUsername") : null
def ossrhPassword = project.hasProperty("ossrhPassword") ?
	project.property("ossrhPassword") : null

signing {
	required {
		gradle.taskGraph.hasTask("uploadArchives")
	}
	sign configurations.archives
}

// see http://central.sonatype.org/pages/gradle.html for details
uploadArchives {

	// prevent the execution for empty (not leaf) subprojects and tests packages
	if(project.name.contains("tests")) {
		return
	}

	repositories {
		mavenDeployer {
			beforeDeployment {
				MavenDeployment deployment -> signing.signPom(deployment)
			}

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			pom {

				groupId = rootProject.group
				name = "nagaina"
				artifactId = name

				project {
					description = rootProject.description

					url "https://github.com/emc-mongoose/nagaina/wiki"

					scm {
						connection "https://github.com/emc-mongoose/nagaina.git"
						developerConnection "https://github.com/emc-mongoose/nagaina.git"
						url "https://github.com/emc-mongoose/nagaina.git"
					}

					licenses {
						license {
							name "The MIT License (MIT)"
							url "https://github.com/emc-mongoose/nagaina/wiki/License"
						}
					}

					developers {
						developer {
							id "akurilov"
							name "Andrey Kurilov"
							email "akurilov123@gmail.com"
						}
					}
				}
			}
		}
	}
}

task dist(type: Tar) {
	group = "distribution"
	description = "Generate Nagaina distribution tarball"
	dependsOn tasks.withType(Jar)
	baseName = rootProject.name
	compression = Compression.GZIP
	destinationDir = new File("build", "dist")
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	into("$baseName-$rootProject.version") {
		into("config") {
			from { "config" }
		}
		into("lib") {
			from configurations.runtime
		}
		from jar.outputs.files
	}
}

String defineVersion() {
	def configFilePath = [rootDir, "config", "defaults.json"].join(File.separator);
	def inputFile = new File(configFilePath)
	def json = new JsonSlurper().parseText(inputFile.text)
	json.version
}
